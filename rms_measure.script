########################################################################################################
##  Adapted from a script by Katherine Crosswhite.
##
##  The script measures the RMS of a given interval on a given tier. The results are 
##  printed to a text file in the directory holding the sound files.
##
##  To run the script, sound files and textgrids must be in the same directory. You need to make sure
##  that the intervals you want to measure are on the same tier in all textgrids. 
##
##  Leendert Plug, August 2012
########################################################################################################

##  The next bit specifies the directory containing the sound files.

directory$ = "\"

##  The next bit makes the header in the output file: it creates a variable called "header_row$", 
##  then writes that variable to the output file. Make sure this matches the analysis output
##  specified below.

header_row$ = "Filename 'tab$' Segment 'tab$' RMS 'newline$'"
header_row$ > 'directory$'rms.txt

##  The next bit makes a list of all the sound files in the directory, and puts the number of
##  filenames into the variable "number_of_files".

Create Strings as file list...  list 'directory$'*.wav
number_files = Get number of strings

## The next bit sets up a "for" loop that will iterate once for every file in the list.

for j from 1 to number_files

## The next bit queries the file-list to get the first filename from it and reads that file in.

     select Strings list
     current_token$ = Get string... 'j'
     Read from file... 'directory$''current_token$'

## The next bit makes a variable called "object_name$" that will be equal to the filename minus 
## the ".wav" extension.

     object_name$ = selected$ ("Sound")

## The next bit gets the corresponding TextGrid and reads it in.

     Read from file... 'directory$''object_name$'.TextGrid

## The next bit queries the TextGrid to find out how many intervals there are on a specified tier (here 1), 
## storing that number in a variable called "number_of_intervals". This is used to set up a "for" loop
## that will go through each of the intervals and do some measurement on it. 

     select TextGrid 'object_name$'
     number_of_intervals = Get number of intervals... 1
     for b from 1 to number_of_intervals
         select TextGrid 'object_name$'
          interval_label$ = Get label of interval... 1 'b'
 
## You can get the script to pay attention to every interval that 
## has a non-null label (the first line), or for an interval with a specific label (here "C", second line).
## If you want the former, activate it by removing ## in front of it, and deactivate the other line by
## putting ## in front of that.

##          if interval_label$ <> ""
            if interval_label$ = "C"
 
## The next bit does the actual measurements and any subsequent calculations.

               begin_segment = Get starting point... 1 'b'
               end_segment = Get end point... 1 'b'
               
               select Sound 'object_name$'
               rms = Get root-mean-square... begin_segment end_segment
       
## Once all the measurements have been done, the results should be appended to the output file the script
## has created. 

            fileappend "'directory$'rms.txt" 'object_name$''tab$''interval_label$''tab$''rms''newline$'
 
         endif
     endfor

## The next bit makes sure the analysis is repeated for each file in the list, removes objects 
## after each analysis, and finally ends the "for" loop. 

     select all
     minus Strings list
     Remove
endfor

## The next bit tidies up and prints a final message.

select all
Remove
clearinfo
print All done.

## end of script